plugins {
    id 'java-library'
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.17.2"
    id "com.jfrog.bintray" version "1.8.5"
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

ext {
    bouncycastleVersion = '1.60'
    jacksonVersion = '2.9.6'
    okhttpVersion = '3.11.0'

    junitVersion = '5.2.0'
    mockitoVersion = '2.19.0'

    mavenCentralSync = true
}

dependencies {
    implementation "org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    testImplementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

test {
    useJUnitPlatform {
        excludeTags "integration"
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags "integration"
    }
    description = 'Runs integration tests.'
    group = 'verification'
}

apply from: '../publish.gradle'
